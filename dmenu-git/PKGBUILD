# Maintainer: Sjors Sparreboom <mail@sirjls.nl>
pkgname=dmenu-git
_pkgname=dmenu
pkgver=r552.2f39898
pkgrel=1
pkgdesc="Dynamic menu X"
arch=("i686" "x86_64")
url="http://suckless.org"
license=('MIT')
groups=("custom")
depends=("sh" "libxinerama" "libxft")
makedepends=('git')
provides=('dmenu')
conflicts=('dmenu')
source=("git://git.suckless.org/${_pkgname}")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${_pkgname}"
        printf "r%s.%s\n" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/${_pkgname}"
	sed \
		-e 's/monospace/DejaVu Sans Mono/g'  \
		-e 's/size=10/size=9/g'  \
		-i config.def.h
}

build() {
	cd "$srcdir/${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "$srcdir/${_pkgname}"
	make PREFIX=/usr DESTDIR="$pkgdir" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
}
